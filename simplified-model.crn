directive simulator deterministic
directive simulation { final=25.0; points=100; plots=[lacI;tetR] }
directive spatial { dimensions = 1; xmax = 1.0; dt = 0.01; nx = 101; diffusibles=[c6=2.0;c12=1.0];  boundary = ZeroFlux }
directive parameters [


  // exclusive loops
  KGR_76 = 1.0;
  KGS_81 = 1.0;
  KR6 = 1;
  KS12 = 1;

  // crosstalk
  KGS_76 = 0;
  KGR_81 = 0;
  KS6 = 0;
  KR12 = 0;

  // activations
  aR=1000; aS=1000;
  aL=100; aT=100;

  // degredations rates
  dL=1000; dT=1000;
  dR=100; dS=100;
]

directive rates [
  PLac = 1/(1+[lacI]);
  PTet = 1/(1+[tetR]);
  P76 = (1 + KGR_76*[boundLuxR] + KGS_76*[boundLasR]) ;
  P81 = (1 + KGR_81*[boundLuxR] + KGS_81*[boundLasR]) ;
  boundLasR = [lasR]^2 * (KS6*[c6]+KS12*[c12]) ;
  boundLuxR = [luxR]^2 * (KR6*[c6]+KR12*[c12])
]

init luxR 0 |
init lasR 0 |
init lacI 0 |
init tetR 0 |
init c6 0 |
init c12 0

| ->[aR*[PTet]] luxR
| luxR ->{dR}

| ->[aS*[PLac]] lasR
| lasR ->{dS}

| ->[aL*[P76]] lacI
| lacI ->{dL}

| ->[aT*[P81]] tetR
| tetR ->{dT}
