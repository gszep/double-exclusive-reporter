directive simulator deterministic
directive simulation { final=25.0; points=100; plots=[lacI;tetR] }
directive spatial { dimensions = 1; xmax = 0.1; dt = 0.01; nx = 101; diffusibles=[c6=0.0000018;c12=0.0000009];  boundary = ZeroFlux }
directive parameters [

  // gene expression capacity
  rc = 67.5;
  growth = 1;

  // baseline inhibition
  a0_76 = 1.92; a0_81 = 0.822;

  // exclusive loops
  KGR_76 = 0.00145301678863517;
  KGS_81 = 1.00086836995962E-05;
  KR6 = 3.50695213045775E-08;
  KS12 = 0.0095893786931253;

  // crosstalk
  KGS_76 = 0;
  KGR_81 = 0;
  KS6 = 0;
  KR12 = 0;

  // hill coefficients
  nR=0.699; nS=1.25;
  nL=1.0; nT=4.0;

  // activations
  aR33=9.1; aS175=3.58;
  aL=0.00117; aT=0.000951;
  a1S = 704; a1R = 1.86e+03;

  // degredations rates
  dL=0.000117;
  dT=0.666;
  dR=0.267;
  dS=0.319;

  // inhibitor sponges
  iA = 0.410196888753378;
  iI = 13.1667354801293;

  ATC = 0.0;
  IPTG = 0.0;
]

directive rates [
  PLac = 1/(1+[lacI]^nL);
  PTet = 1/(1+[tetR]^nT);
  P76 = (a0_76 + a1R*KGR_76*[boundLuxR] + a1S*KGS_76*[boundLasR]) / (1+KGR_76*[boundLuxR]+KGS_76*[boundLasR]);
  P81 = (a0_81 + a1R*KGR_81*[boundLuxR] + a1S*KGS_81*[boundLasR]) / (1+KGR_81*[boundLuxR]+KGS_81*[boundLasR]);
  boundLasR = [lasR]^2 * ((KS6*[c6])^nS + (KS12*[c12])^nS) / ((1+KS6*[c6]+KS12*[c12])^nS);
  boundLuxR = [luxR]^2 * ((KR6*[c6])^nR + (KR12*[c12])^nR) / ((1+KR6*[c6]+KR12*[c12])^nR)
]

init luxR 0 |
init lasR 0 |
init lacI 0 |
init tetR 0 |
init c6 20 |
init c12 120

| ->[rc*aR33*[PTet]] luxR
| luxR ->{dR}
| luxR ->[growth*[luxR]]

| ->[rc*aS175*[PLac]] lasR
| lasR ->{dS}
| lasR ->[growth*[lasR]]

| ->[rc*aL*[P76]] lacI
| lacI ->{dL}
| lacI ->[growth*[lacI]]

| ->[rc*aT*[P81]] tetR
| tetR ->{dT}
| tetR ->[growth*[tetR]]


| lacI ->{iI*IPTG}
| tetR ->{iA*ATC}
