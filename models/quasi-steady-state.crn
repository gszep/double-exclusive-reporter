directive simulation { final=24; points=250; plots=[yfp;cfp]; }
directive spatial { dimensions = 1; xmax = 0.07; dt = 0.007; nx = 101; diffusibles=[c6=0.0000018;c12=0.0000009];  boundary = ZeroFlux }
directive parameters [

  // growth parameters
  capacity = 1.0;
  r = 0.5;

  // signal affinity and crosstalk
  KGR_76 = 0.0342;
  KGS_76 = 0.000165;
  KGR_81 = 0.000742;
  KGS_81 = 10.4;
  eS6 = 1.01e-8;
  eR12 = 4.2e-7;

  // hill coefficients
  nR = 0.618;
  nS = 0.324;
  nL = 0.58;
  nT = 1.65;

  // activations
  aR33 = 16.7;
  aS175 = 2.86;
  aL = 3480.0;
  aT = 67.4;
  aCFP = 2.16E+04;
  aYFP = 1.62E+04;

  e76 = 0.000114;
  e81 = 0.000267;

  // degredations
  dR = 24.6;
  dS = 8.58;
  dL = 1.0;
  dT = 1.0;
  dCFP = 0.379;
  dYFP = 0.14;

  // derepressors
  iA = 0.421;
  iI = 418.0;
  ATC = 0.0;
  IPTG = 0.0;

  // relays
  kC12 = 300.0;
  Klas = 48080.5179863686;
  dlasI = 0.000024793285232104;

  kC6 = 0.0; //600.788053105;
  Klux = 68995.5461317435;
  dluxI = 9.18489433188243;
]

directive rates [
  BS = [lasR]^2 * ( (eS6 *[c6] )^nS + [c12]^nS );
  BR = [luxR]^2 * ( (eR12*[c12])^nR + [c6]^nR );
  P76 = ( e76 + KGR_76*[BR] + KGS_76*[BS] ) / ( 1 + KGR_76*[BR] + KGS_76*[BS] );
  P81 = ( e81 + KGR_81*[BR] + KGS_81*[BS] ) / ( 1 + KGR_81*[BR] + KGS_81*[BS] );
  PLac = 1 / (1 + [lacI]^nL );
  PTet = 1 / (1 + [tetR]^nT );
  growth = r*(1-rho)
]

| init c6 0
| init c12 0
| init rho 0.002

| -> [[rho]*[growth]] rho

| ->[(([capacity] * aR33) * [PTet])] luxR
| luxR ->{dR}
| luxR ->[([growth] * [luxR])]

| ->[(([capacity] * aS175) * [PLac])] lasR
| lasR ->{dS}
| lasR ->[([growth] * [lasR])]

| ->[(([capacity] * aT) * [P81])] tetR
| tetR ->{dT}
| tetR ->[([growth] * [tetR])]
| tetR ->{(iA * ATC)}

| ->[(([capacity] * aL) * [P76])] lacI
| lacI ->[([growth] * [lacI])]
| lacI ->{dL}
| lacI ->{(iI * IPTG)}

| ->[(([capacity] * aYFP) * [P81])] yfp
| yfp ->{dYFP}
| yfp ->[([growth] * [yfp])]

| ->[(([capacity] * aCFP) * [P76])] cfp
| cfp ->{dCFP}
| cfp ->[([growth] * [cfp])]

| ->[[capacity]*[P81]] luxI
| luxI ->{dluxI}
| ->[kC6*[luxI]*[rho]] c6

| ->[[capacity]*[P76]] lasI
| lasI ->{dlasI}
| ->[kC12*[lasI]*[rho]] c12
